wrapper {
    gradleVersion = '6.5.1'
}

apply plugin: 'java-library'
apply plugin: 'eclipse'
apply plugin: 'maven-publish'

group = pGroup
version = pVersion

if (project.hasProperty("topobyte")) {
    apply from: 'file:///' + System.getProperty("user.home") \
        + '/.gradle/topobyte.gradle'
}

repositories {
    maven {
        url 'https://mvn.topobyte.de'
    }
    maven {
        url 'https://mvn.slimjars.com'
    }
    mavenCentral()
}

sourceCompatibility = 1.8
targetCompatibility = 1.8

dependencies {
    api 'de.topobyte:luqe-core:0.2.1'
    api 'de.topobyte:jsqltables:0.1.0'
    testImplementation 'de.topobyte:luqe-jdbc:0.2.1'
    testRuntimeOnly 'org.xerial:sqlite-jdbc:3.27.2.1'

    testImplementation 'junit:junit:4.13'
    testRuntimeOnly 'ch.qos.logback:logback-classic:1.2.3'
}

java {
    withSourcesJar()
}

def pomInfo = new XmlSlurper().parse(file('info.pom'));

publishing {
    publications {
        maven(MavenPublication) {
            from components.java
            pom {
                name = "$pomInfo.name"
                description = "$pomInfo.description"
                url = "$pomInfo.url"
                licenses {
                    license {
                        name = "$pomInfo.licenseName"
                        url = "$pomInfo.licenseUrl"
                        distribution = "$pomInfo.licenseDistribution"
                    }
                }
            }
        }
    }
}
